#!/usr/bin/env groovy
// Licensed to Elasticsearch B.V. under one or more contributor
// license agreements. See the NOTICE file distributed with
// this work for additional information regarding copyright
// ownership. Elasticsearch B.V. licenses this file to you under
// the Apache License, Version 2.0 (the "License"); you may
// not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.
@Library('apm@current') _

import groovy.transform.Field

// VM names created to run later on the terraform plan
// key is the cluster name and value is the VM name
@Field def vms = [:]

// Passed stages to workaround the matrix/post-build
// See https://issues.jenkins.io/browse/JENKINS-65997
@Field def successStages = [:]

pipeline {
  agent none
  environment {
    REPO = "azure-vm-extension"
    NOTIFY_TO = credentials('notify-to')
    PIPELINE_LOG_LEVEL = 'INFO'
    LANG = "C.UTF-8"
    LC_ALL = "C.UTF-8"
    ELASTIC_STACK_VERSION = '7.14.1'
  }
  options {
    buildDiscarder(logRotator(numToKeepStr: '5', artifactNumToKeepStr: '5', daysToKeepStr: '7'))
    timestamps()
    ansiColor('xterm')
    disableResume()
    durabilityHint('PERFORMANCE_OPTIMIZED')
    timeout(time: 2, unit: 'HOURS')
    disableConcurrentBuilds()
  }
  triggers {
    issueCommentTrigger("${obltGitHubComments()}")
  }
  parameters {
    booleanParam(name: 'skipDestroy', defaultValue: "false", description: "Whether to skip the destroy of the cluster and terraform.")
  }
  stages {
    stage('ITs') {
      options { skipDefaultCheckout() }
      failFast false
      matrix {
        agent {label "${OS_VERSION}"}
        axes {
          axis {
            name 'OS_VERSION'
            values 'ubuntu-18' // TODO: support windows, 'windows-2016 && windows-immutable'
          }
        }
        environment {
          HOME = "${env.WORKSPACE}"
          PATH = "${env.HOME}/bin:${env.PATH}"
        }
        stages {
          stage('Checkout'){
            options { skipDefaultCheckout() }
            steps {
              deleteDir()
              checkout scm
            }
          }
          stage('Create cluster'){
            options { skipDefaultCheckout() }
            environment {
              STAGE_NAME = "Create cluster|${ELASTIC_STACK_VERSION}|${OS_VERSION}"
            }
            steps {
              withGithubNotify(context: "Create Cluster ${ELASTIC_STACK_VERSION} ${OS_VERSION}") {
                withVaultEnv(){
                  sh(label: 'Deploy Cluster', script: 'make -C .ci create-cluster')
                }
              }
              successStage(env.STAGE_NAME)
            }
            post {
              failure {
                whenFalse(isSuccessStage(env.STAGE_NAME)) {
                  destroyCluster()
                }
              }
            }
          }
          stage('Prepare tools') {
            options { skipDefaultCheckout() }
            environment {
              STAGE_NAME = "Tools|${ELASTIC_STACK_VERSION}|${OS_VERSION}"
            }
            steps {
              withCloudEnv() {
                sh(label: 'Prepare tools', script: 'make -C .ci prepare')
              }
              successStage(env.STAGE_NAME)
            }
            post {
              failure {
                whenFalse(isSuccessStage(env.STAGE_NAME)) {
                  destroyCluster()
                }
              }
            }
          }
          stage('Prepare context') {
            options { skipDefaultCheckout() }
            environment {
              STAGE_NAME = "Context|${ELASTIC_STACK_VERSION}|${OS_VERSION}"
            }
            steps {
              withGithubNotify(context: "Context ${ELASTIC_STACK_VERSION} ${OS_VERSION}") {
                withCloudEnv() {
                  sh(label: 'Prepare context', script: 'sudo make -C .ci prepare-pr-context')
                }
              }
              successStage(env.STAGE_NAME)
            }
            post {
              failure {
                whenFalse(isSuccessStage(env.STAGE_NAME)) {
                  destroyCluster()
                }
              }
            }
          }
          stage('Validate Install') {
            options { skipDefaultCheckout() }
            steps {
              withGithubNotify(context: "Validate Install ${ELASTIC_STACK_VERSION} ${OS_VERSION}") {
                withValidationEnv() {
                  sh(label: 'Validate PR install', script: 'sudo make -C .ci prepare-pr-install')
                  sh(label: 'Validate install', script: 'make -C .ci validate-install')
                }
              }
            }
            post {
              always {
                junit(allowEmptyResults: true, keepLongStdio: true, testResults: 'test/ats/TEST-*.xml')
                destroyCluster()
              }
            }
          }
          stage('Validate Uninstall') {
            options { skipDefaultCheckout() }
            steps {
              withGithubNotify(context: "Validate Uninstall ${ELASTIC_STACK_VERSION} ${OS_VERSION}") {
                withValidationEnv() {
                  sh(label: 'Validate PR uninstall', script: 'sudo make -C .ci prepare-pr-uninstall')
                  sh(label: 'Validate uninstall', script: 'make -C .ci validate-uninstall')
                }
              }
            }
            post {
              always {
                junit(allowEmptyResults: true, keepLongStdio: true, testResults: 'test/ats/TEST-*.xml')
                destroyCluster()
              }
            }
          }
        }
      }
    }
  }
  post {
    cleanup {
      notifyBuildResult(prComment: true)
    }
  }
}

def successStage(String name) {
  successStages[name] = true
}

def isSuccessStage(String name) {
  return successStages.containsKey(name)
}

def destroyCluster( ) {
  if (params.skipDestroy) {
    echo 'Skipped the destroy cluster step'
    return
  }
  withVaultEnv(){
    sh(label: 'Destroy Cluster', script: 'make -C .ci destroy-cluster')
  }
}

def withVaultEnv(Closure body){
  getVaultSecret.readSecretWrapper {
    withMatrixEnv() {
      withEnvMask(vars: [
        [var: 'VAULT_ADDR', password: env.VAULT_ADDR],
        [var: 'VAULT_ROLE_ID', password: env.VAULT_ROLE_ID],
        [var: 'VAULT_SECRET_ID', password: env.VAULT_SECRET_ID],
        [var: 'VAULT_AUTH_METHOD', password: 'approle'],
        [var: 'VAULT_AUTHTYPE', password: 'approle']
      ]){
        body()
      }
    }
  }
}

def withValidationEnv(Closure body) {
  withMatrixEnv() {
    withClusterEnv(cluster: env.CLUSTER_NAME) {
      body()
    }
  }
}

def withCloudEnv(Closure body) {
  withMatrixEnv() {
    withCloudEnv(cluster: env.CLUSTER_NAME) {
      // withCloudEnv creates different env variables, let's create the
      // ones needed for the terraform runs
      withEnvMask(vars: [
        [var: 'TF_VAR_username', password: env.CLOUD_USERNAME],
        [var: 'TF_VAR_password', password: env.CLOUD_PASSWORD],
        [var: 'TF_VAR_cloudId', password: env.CLOUD_ID]
      ]){
        body()
      }
    }
  }
}

def withAzEnv(Closure body) {
  withMatrixEnv() {
    withAzureEnv(secret: 'secret/observability-team/ci/service-account/azure-vm-extension') {
      body()
    }
  }
}

def withMatrixEnv(Closure body) {
  def clusterName = "tst-az-${BUILD_ID}-${BRANCH_NAME}-${ELASTIC_STACK_VERSION}-${env.OS_VERSION}"
  def vmName = getCachedVmNameOrAssignVmName(clusterName)
  withEnv([
    "CLUSTER_NAME=${clusterName}",
    'TF_VAR_prefix=tst-' + vmName.take(6),
    "TF_VAR_vmName=${vmName}",
    "TF_VAR_isWindows=${isWindows()}",
    "VM_NAME=${vmName}"
  ]) {
    echo "CLUSTER_NAME=${CLUSTER_NAME} - VM_NAME=${VM_NAME} - TF_VAR_prefix=${TF_VAR_prefix} - TF_VAR_isWindows=${TF_VAR_isWindows}"
    body()
  }
}

def getCachedVmNameOrAssignVmName(String key) {
  if (vms.containsKey(key)) {
    return vms.get(key)
  } else {
    def vmName = randomString(size: 15)
    vms[key] = vmName
    return vmName
  }
}

def isWindows() {
  return env.OS_VERSION?.toLowerCase()?.contains('windows')
}
